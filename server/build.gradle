plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '12.1.9'
    id 'com.github.spotbugs' version '6.4.8'
}

group = 'com.amogus'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

configurations.all {
    resolutionStrategy {
        force 'org.apache.commons:commons-lang3:3.18.0'
        force 'org.apache.httpcomponents.client5:httpclient5:5.4.3'
        force 'org.apache.tomcat.embed:tomcat-embed-core:10.1.47'
        force 'org.apache.tomcat.embed:tomcat-embed-websocket:10.1.47'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.postgresql:postgresql:42.7.7'
    implementation 'org.liquibase:liquibase-core'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
    implementation 'org.webjars:swagger-ui:5.20.0'

    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // Spring Boot Starter Web
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Spring Boot Starter Security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // Observability
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'net.ttddyy.observation:datasource-micrometer-spring-boot:1.0.6'

    // JWT Library
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5' // for JSON processing


    // Lombok (optional for reducing boilerplate code)
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // SpotBugs + FindSecBugs
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
    spotbugs 'com.github.spotbugs:spotbugs:4.8.6'
    spotbugs 'org.apache.commons:commons-lang3:3.18.0'
    spotbugs 'org.apache.httpcomponents.client5:httpclient5:5.4.3'

    implementation 'org.apache.commons:commons-lang3:3.18.0'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.4.3'
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:10.1.47'
    implementation 'org.apache.tomcat.embed:tomcat-embed-websocket:10.1.47'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy(tasks.jacocoTestReport)
}

tasks.named("bootRun") {
	mainClass = 'com.amogus.server.ServerApplication'
}

jacoco {
    toolVersion = "0.8.12"
}

def coverageExclusions = [
        '**/config/**',
        '**/security/**',
        '**/models/**',
        '**/repositories/**',
        '**/payload/**',
        '**/controllers/**',
        '**/events/**',
        '**/aspects/**',
        '**/ServerApplication**'
]

def jacocoMainClasses = fileTree(dir: "$buildDir/classes/java/main", exclude: coverageExclusions)

tasks.jacocoTestReport {
    dependsOn tasks.test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    classDirectories.setFrom(jacocoMainClasses)
}

tasks.jacocoTestCoverageVerification {
    dependsOn tasks.test
    classDirectories.setFrom(jacocoMainClasses)
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

tasks.check {
    dependsOn tasks.jacocoTestCoverageVerification
    dependsOn tasks.dependencyCheckAnalyze
    dependsOn tasks.spotbugsMain
    dependsOn tasks.spotbugsTest
    dependsOn 'spotbugsHighSecurityCheck'
}

def nvdApiKey = System.getenv('NVD_API_KEY')

dependencyCheck {
    failBuildOnCVSS = 7.0
    nvd {
        apiKey = nvdApiKey
        delay = 16000
    }
}

tasks.named('dependencyCheckAnalyze') {
    doFirst {
        if (nvdApiKey == null || nvdApiKey.trim().isEmpty()) {
            throw new GradleException('NVD_API_KEY is required for OWASP Dependency-Check. Set it in your environment before running Gradle.')
        }
    }
}

spotbugs {
    toolVersion = '4.8.6'
    effort.set(com.github.spotbugs.snom.Effort.MAX)
    reportLevel.set(com.github.spotbugs.snom.Confidence.valueOf('LOW'))
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    extraArgs.add('-high')
    reports {
        html.required = true
        xml.required = true
    }
}

tasks.named('spotbugsMain', com.github.spotbugs.snom.SpotBugsTask) {
    auxClassPaths.from(sourceSets.main.runtimeClasspath)
    auxClassPaths.from(files("${System.getProperty('java.home')}/lib/jrt-fs.jar"))
}

tasks.named('spotbugsTest', com.github.spotbugs.snom.SpotBugsTask) {
    auxClassPaths.from(sourceSets.test.runtimeClasspath)
    auxClassPaths.from(files("${System.getProperty('java.home')}/lib/jrt-fs.jar"))
}

tasks.register('spotbugsHighSecurityCheck') {
    dependsOn tasks.spotbugsMain
    doLast {
        def reportFile = file("$buildDir/reports/spotbugs/main.xml")
        if (!reportFile.exists()) {
            throw new GradleException("SpotBugs report not found at ${reportFile}.")
        }
        def reportText = reportFile.getText('UTF-8')
        def matcher = reportText =~ /<BugInstance[^>]*category=\"SECURITY\"[^>]*priority=\"1\"/
        if (matcher.find()) {
            throw new GradleException("SpotBugs reported HIGH severity security issues. See $buildDir/reports/spotbugs/main.html")
        }
    }
}
